#!/usr/bin/env perl

use Mojolicious::Lite;

use lib 'lib';

plugin 'DBAuth';
plugin 'bcrypt';  # Used in DBAuth

get '/' => sub { shift->render } => 'index';

post '/login' => sub {
    my $self = shift;

    if ( $self->auth ) {
        $self->session( auth => 1 );
        return $self->redirect_to('auth');
    }

    $self->flash( error => 'Invalid login' );
    $self->redirect_to('index');
} => 'login';

get '/logout' => sub {
    my $self = shift;

    delete $self->session->{auth};

    $self->redirect_to('index');
} => 'logout';

under sub {
    my ($self) = @_;

    my $session = $self->session('auth') // '';

    return 1
        if $session eq '1';

    $self->render( text => 'Denied!' );
    return 0;
};

get '/auth' => sub { shift->render( text => 'Welcome!' ) };

app->start;

__DATA__

@@index.html.ep
%= tag h1 => 'Login'
% if ( flash('error') ) {
  <h2 style="color:red"><%= flash('error') %></h2>
% }
%= form_for login => (method => 'post') => begin
    Username: <%= text_field 'username' %>
    Password: <%= password_field 'password' %>
    %= submit_button 'Login'
%= end
