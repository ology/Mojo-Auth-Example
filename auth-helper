#!/usr/bin/env perl

use Mojolicious::Lite;

use lib 'lib';

app->secrets(['I am the walrus.']);

plugin 'DBAuth';
plugin 'bcrypt';

get '/' => sub { shift->render } => 'index';

post '/login' => sub {
    my ($self) = @_;

    if ( $self->auth ) {
        $self->session( auth => 1 );
        return $self->redirect_to('auth');
    }

    $self->flash( error => 'Invalid login' );
    $self->redirect_to('index');
} => 'login';

get '/logout' => sub {
    my ($self) = @_;

    delete $self->session->{auth};

    $self->redirect_to('index');
} => 'logout';

under sub {
    my ($self) = @_;

    my $session = $self->session('auth') // '';

    return 1
        if $session eq '1';

    $self->render( text => 'Denied!' );
    return 0;
};

get '/auth' => sub { shift->render } => 'auth';

post '/add' => sub {
    my ($self) = @_;

    my $result = $self->add();

    if ( $result ) {
        $self->flash( message => 'User added' )
    }
    else {
        $self->flash( error => 'Cannot add user!' )
    }

    $self->redirect_to('auth');
};

app->start;

__DATA__

@@_credentials.html.ep
    %= label_for username => 'Username:'
    %= text_field 'username'
    %= label_for password => 'Password:'
    %= password_field 'password'

@@_flash.html.ep
% if ( flash('error') ) {
    %= tag h2 => (style => 'color:red') => flash('error')
% }
% if ( flash('message') ) {
    %= tag h2 => (style => 'color:green') => flash('message')
% }

@@index.html.ep
%= tag h1 => 'Login'
%= include '_flash'
%= form_for login => (method => 'post') => begin
    %= include '_credentials'
    %= submit_button 'Login'
%= end

@@auth.html.ep
%= tag h1 => 'Authorized'
%= include '_flash'
New user:
%= form_for add => (method => 'post') => begin
    %= include '_credentials'
    %= submit_button 'Add'
%= end
%= link_to Logout => 'logout'
