#!/usr/bin/env perl

use Mojolicious::Lite;

use lib 'lib';

plugin 'DBAuth';
plugin 'bcrypt';  # Used in DBAuth

get '/' => sub { shift->render } => 'index';

post '/login' => sub {
    my ($self) = @_;

    if ( $self->auth ) {
        $self->session( auth => 1 );
        return $self->redirect_to('auth');
    }

    $self->flash( error => 'Invalid login' );
    $self->redirect_to('index');
} => 'login';

get '/logout' => sub {
    my ($self) = @_;

    delete $self->session->{auth};

    $self->redirect_to('index');
} => 'logout';

under sub {
    my ($self) = @_;

    my $session = $self->session('auth') // '';

    return 1
        if $session eq '1';

    $self->render( text => 'Denied!' );
    return 0;
};

get '/auth' => sub { shift->render } => 'auth';

post '/add' => sub {
    my ($self) = @_;

    my $result = $self->add();

    if ( $result ) {
        $self->flash( message => 'User added' )
    }
    else {
        $self->flash( error => 'Cannot add user!' )
    }

    $self->redirect_to('auth');
};

app->start;

__DATA__

@@index.html.ep
%= tag h1 => 'Login'
% if ( flash('error') ) {
    %= tag h2 => (style => 'color:red') => flash('error')
% }
%= form_for login => (method => 'post') => begin
    Username: <%= text_field 'username' %>
    Password: <%= password_field 'password' %>
    %= submit_button 'Login'
%= end

@@auth.html.ep
%= tag h1 => 'Authorized'
% if ( flash('error') ) {
    %= tag h2 => (style => 'color:red') => flash('error')
% }
% if ( flash('message') ) {
    %= tag h2 => (style => 'color:green') => flash('message')
% }
New user:
%= form_for add => (method => 'post') => begin
    Username: <%= text_field 'username' %>
    Password: <%= password_field 'password' %>
    %= submit_button 'Add'
%= end
%= link_to Logout => 'logout'
